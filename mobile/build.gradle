apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "liou.rayyuan.phenom"
        targetSdkVersion 23
        versionCode getGitVersionCodeTimeStamps()
        versionName "0.1"
        minSdkVersion 21

        buildConfigField "String", "PLURK_APPKEY", app_key
        buildConfigField "String", "PLURK_APPSECRET", app_secret
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled true
    }
}

def getGitVersionCodeTimeStamps() {
    def date = new Date()
    def formatedDate = date.format("yyMMddHHmm")
    def code = formatedDate.toInteger()

    return code
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:design:${support_version}"
    compile "com.squareup.retrofit2:retrofit:${retrofit_version}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit_version}"
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.artemzin.rxjava:proguard-rules:1.1.3.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'com.facebook.fresco:fresco:0.10.0'
    compile 'se.akerfeldt:okhttp-signpost:1.1.0'
    compile ('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.9.1@aar'){
        transitive=true
    }
}
